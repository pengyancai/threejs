<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>彭延财-use_custom_shader</title>
</head>
<body>

<!--vertex shader-->
<script type="x-shader/x-vertex" id="baseVertex">
    #ifdef GL_ES
    precision highp float;
    #endif
    #define pi 3.1415926535897
    #define pi2 6.28318530717958
    attribute vec3 ph_tangent;
    attribute vec3 ph_binormal;
    attribute vec2 ph_uv0;
    varying highp vec3 vTangent;
    varying highp vec3 vBitang;
    varying highp vec3 vNormal;
    varying vec2 vUv;
    varying vec3 vLightPos;
    varying vec3 vViewPosition;

    uniform float _elapsedTime;

    void main()
    {
        vUv = ph_uv0;

        vec3 normal3p = normal;
        vec3 tangent3p = ph_tangent.xyz;
        vec3 binormal3p = -ph_binormal;

        vNormal = normalize( normal3p );
        vTangent = normalize( binormal3p );
        vBitang = normalize( tangent3p );
        vec4 wPos = modelMatrix * vec4( position, 1.0 );
        vViewPosition = cameraPosition - wPos.xyz;
        vec2 circleMotion;
        circleMotion.x = cos(_elapsedTime*pi2);
        circleMotion.y = sin(_elapsedTime*pi2);
        vLightPos = vec3( circleMotion.x,.8,circleMotion.y ); //world
        vLightPos = normalize(vLightPos);
        vLightPos = (viewMatrix * vec4(vLightPos,0.0)).xyz;
        gl_Position = projectionMatrix * viewMatrix * wPos;
    }
</script>
<!--frag shader-->
<script type="x-shader/x-fragment" id="surfaceFancy">
    #ifdef GL_ES
        precision highp float;
    #endif

    #ifdef GL_OES_standard_derivatives

        #extension GL_OES_standard_derivatives : enable

    #endif
    #define PI2 6.28318530717959;
    //normal map
    varying highp vec3 vTangent;
    varying highp vec3 vBitang;
    varying highp vec3 vNormal;

    varying vec2 vUv;

    //single light
    varying vec3 vLightPos;
    varying vec3 vViewPosition;

    //textures
    uniform sampler2D _texNormal;
    uniform sampler2D _texDiffuse;
    uniform sampler2D _texSpec;

    uniform samplerCube _cubeAmb;
    uniform samplerCube _cubeSpec;
    uniform float _ambGamma;
    uniform float _ambPow;

    uniform float _refGamma;
    uniform float _refPower;

    //various
    uniform vec3 _color;
    uniform vec3 _specColor;

    uniform mat3 _normalMatrix;

    uniform float _specPower;
    uniform float _glossiness;
    uniform	float _lightIntensity;
    uniform float _refRPow;
    uniform float _refRBias;
    uniform	float _rimIntens;
    uniform int _choiceNormal;
    uniform int _choiceOutput;
    #ifdef SAMPLE_NUM
    uniform	vec4 _uPhongRands[SAMPLE_NUM];
    #else
    uniform vec4 _uPhongRands[1];
    #endif

    uniform sampler2D _noiseTex;
    uniform sampler2D _sampleTex;
    uniform vec4 _screenStuff;
    uniform float _specAdjust;

    uniform float _mipBias;

    vec3 Fschlick( vec3 cSpec , vec3 V , vec3 H )
    {
        return  vec3( cSpec + (1.0 - cSpec) * (1.0 - pow( dot( V, H ), 5.0)) );
    }
    float Fschlick( float fres , vec3 V, vec3 H )
    {
        return  float( fres + (1.0 - fres) * (1.0 - pow( dot( V, H ), 5.0)) );
    }

    float Mfresnel(vec3 eye, vec3 normal, float reflectivity, float fresnelStrength )
    {
        //schlick's fresnel approximation
        float f = 1.0 - clamp( dot( eye, normal ), 0.0, 1.0 );
        float f2 = f*f;
        f *= f2 * f2;
        return mix( reflectivity, 1.0, f*fresnelStrength );
    }

    vec3 phongImportanceSample( vec4 r, float specExp ){

        float cos_theta = pow( r.x, 1.0 / (specExp + 1.0) );
        float sin_theta = sqrt( 1.0 - cos_theta * cos_theta );
        float cos_phi = r.z;
        float sin_phi = r.w;
        return	vec3( cos_phi*sin_theta, sin_phi*sin_theta, cos_theta );
    }

    void main()
    {
        if(_choiceOutput == 3){
            gl_FragColor.xyz = vec3(vUv, 0.0);
            return;
        }
        vec2 screenCoord = gl_FragCoord.xy * _screenStuff.xy;

        vec3 lightColor = vec3(0.0);
        vec3 finalOutput = vec3(0.0);
        vec4 diffuseTexture = texture2D( _texDiffuse, vUv);
        vec3 specTexture = texture2D( _texSpec, vUv).xyz;
        vec3 normalMap = texture2D(_texNormal, vUv).xyz;

        vec3 finalNormal;
        vec3 bitangent3p, tangent3p, normal3p;

        if(_choiceNormal == 1){

            normalMap = normalMap * 2.0 - 1.0;
            bitangent3p = inversesqrt ( dot( vBitang, vBitang) ) * vBitang;
            finalNormal = normalMap.x * bitangent3p;
            tangent3p = inversesqrt ( dot( vTangent, vTangent) ) * vTangent;
            finalNormal = finalNormal + normalMap.y*tangent3p;
            normal3p = inversesqrt ( dot( vNormal, vNormal) )*vNormal;
            finalNormal = finalNormal + normalMap.z*normal3p;
            finalNormal = inversesqrt( dot( finalNormal, finalNormal) ) * finalNormal;

        } else {
            finalNormal = normalize(vNormal);
        }

        if(_choiceOutput == 2){
            gl_FragColor.xyz = finalNormal;
            return;
        } else if (_choiceOutput == 1){
            gl_FragColor.xyz = ( viewMatrix * vec4(finalNormal, 0.0)).xyz;
            return;
        }
        vec3 lightDir = normalize(vLightPos);
        vec3 viewDir = normalize( vViewPosition );

        float NdotL = dot(finalNormal, lightDir);
        NdotL = clamp(NdotL,0.0, 1.0);

        // lightColor+=NdotL;
        specTexture.xyz *= _specAdjust;
        specTexture.xyz = clamp(specTexture.xyz, 0.0, 1.0);

        vec3 h = normalize(lightDir + viewDir);
        float spec = clamp( dot( finalNormal, h), 0.0, 1.0);

        float specF =  Fschlick( _refRPow , viewDir, h );
        float factor = 0.0397436 * _glossiness + 0.0856832;
        specF *= factor ;
        specF *= pow( spec, _glossiness );

        specF *= NdotL;

        specF *= _specPower;

        NdotL *= _lightIntensity;


        float gloss = specTexture.x*_mipBias;
        float specExp = -10.0 / log2( gloss*0.968 + 0.03 );
        specExp *= specExp;

        vec3 basisX = normalize(
            cross(
                finalNormal,
                vec3(
                    0.0,
                    1.0,
                    finalNormal.y
                )
            )
        );
        vec3 basisY = cross( basisX, finalNormal );
        vec3 basisZ = finalNormal;
        vec3 specccc = vec3(0.0, 0.0, 0.0);

        vec4 r1 = texture2D( _noiseTex, screenCoord);
        vec2 normScr = gl_FragCoord.xy * _screenStuff.zw;

        vec2 seed = vec2(

            floor(256.0 * normScr.x)  			* r1.x +
                floor(65536.0 * normScr.y) 		* r1.y,
            floor(256.0 * normScr.x) 			* r1.z +
                floor(65536.0 * normScr.y) 		* r1.w

        );
        vec2 test = vec2(0.0);
        float tt = 0.0;//dodaj ovde random iz teksture

        for( int i=0; i < SAMPLE_NUM; ++i )
        {
            vec4 r = texture2D( _sampleTex, seed + float(i) * 0.015625 );
            tt = r.x;
            r.zw = r.zw * 2.0 - 1.0;
            vec3 dir = phongImportanceSample(r, specExp);
            vec3 h = dir.x*basisX + dir.y*basisY + dir.z*basisZ;

            float pdf = (specExp * 0.039788735773 + 6.283185307175) *
                pow(
                    clamp(
                        dot( h, finalNormal),
                        0.0,
                        1.0
                    ),
                    specExp
                );
            vec3 lookup = reflect( viewDir, h );
            lookup.y = -lookup.y;

            specccc += SAMPLE_NUM_INV * textureCube( _cubeSpec, lookup).xyz;
        }

        #ifdef GL_EXT_shader_texture_lod
            gl_FragColor = vec4( vec3( specccc ) , 1.0);
            return;
        #endif

        #ifdef GL_OES_standard_derivatives

            vec3 bbb = specccc + dFdx( specccc );
            bbb += specccc;

            vec3 aaa = specccc + dFdy( specccc );
            aaa += specccc;
            bbb += aaa;
            bbb *= .25;
            specccc = bbb;
        #endif

        specccc = pow(specccc, vec3( _refGamma ));

        specccc *= _refPower;
        if(_choiceOutput == 5){
            gl_FragColor.xyz = specccc;
            return;
        }

        float glossAdjust = gloss * gloss;

        specccc *= Mfresnel(
            viewDir,
            finalNormal,
            _refRBias,
            glossAdjust
        );

        vec3 ambCube = textureCube( _cubeAmb, vec3( finalNormal )).xyz;

        ambCube = pow(ambCube, vec3(_ambGamma));
        ambCube *= _ambPow;

        finalOutput = diffuseTexture.xyz * (ambCube + NdotL);
        finalOutput += ( specccc + specF ) * specTexture;

        gl_FragColor = vec4( finalOutput, 1.0);
    }
</script>

<script type='text/javascript' src="js/libs/jquery.min.js" ></script>
<script type='text/javascript' src="js/libs/Three.js" ></script>
<script type='text/javascript' src="js/use_custom_shader.js" ></script>
<script type='text/javascript' src="js/libs/createNoise.js" ></script>
<script type='text/javascript' src="js/libs/createPatt.js" ></script>
<script type='text/javascript' src="js/libs/Detector.js" ></script>
<script type='text/javascript' src="js/libs/pailhead_loader_final.js" ></script>

</body>
</html>